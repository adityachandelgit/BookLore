plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.hibernate.orm' version '6.6.10.Final'
}

ext {
    testcontainersVersion = '1.20.6'
}

group = 'com.adityachandel'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    // --- Spring Boot ---
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // --- Database & Migration ---
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.5.2'
    implementation 'org.flywaydb:flyway-mysql:11.3.4'

    // --- Security & Authentication ---
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // --- Lombok (For Clean Code) ---
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    // --- Book & Image Processing ---
    implementation 'org.apache.pdfbox:pdfbox:3.0.4'
    implementation 'org.apache.pdfbox:jbig2-imageio:3.0.4'
    implementation 'com.github.jai-imageio:jai-imageio-core:1.4.0'
    implementation 'com.github.jai-imageio:jai-imageio-jpeg2000:1.4.0'
    implementation 'io.documentnode:epub4j-core:4.2.2'

    // --- JSON & Web Scraping ---
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.18.3'
    implementation 'org.jsoup:jsoup:1.19.1'

    // --- Mapping (DTOs & Entities) ---
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    // --- API Documentation ---
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.5'

    // Test Dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core'
    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation "org.testcontainers:mariadb:${testcontainersVersion}"
}

hibernate {
    enhancement {
        enableAssociationManagement = true
    }
}

test {
    useJUnitPlatform()
}